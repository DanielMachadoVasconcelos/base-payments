plugins {
	id 'java'
	id 'idea'
	id 'jacoco'

	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.ead.payments'
version = '0.0.1-SNAPSHOT'

tasks.withType(JavaCompile) {
	options.compilerArgs += '--enable-preview'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(24)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

ext {
	set('springModulithVersion', "1.4.0")
	set('springCloudVersion', "2025.0.0")
}

dependencies {

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	implementation 'org.springframework.modulith:spring-modulith-events-kafka'
	implementation 'org.springframework.modulith:spring-modulith-events-api'
	implementation 'org.springframework.modulith:spring-modulith-starter-core'
	implementation 'org.springframework.modulith:spring-modulith-starter-jpa'

	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	implementation 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	implementation 'org.springdoc:springdoc-openapi-ui:1.6.11'

	runtimeOnly 'org.postgresql:postgresql'

	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'

	implementation 'io.micrometer:micrometer-core'
	implementation 'io.micrometer:micrometer-observation'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'io.micrometer:micrometer-tracing'
	implementation 'io.micrometer:micrometer-tracing-bridge-brave'
	testImplementation 'io.micrometer:micrometer-observation-test'

	implementation 'org.wiremock.integrations:wiremock-spring-boot:3.6.0'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-docker-compose'
	testImplementation 'org.springframework.modulith:spring-modulith-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	testImplementation 'com.tngtech.archunit:archunit-junit5:1.4.1'
	testImplementation 'com.tngtech.archunit:archunit-junit5-api:1.4.1'
	testImplementation 'com.tngtech.archunit:archunit:1.4.1'

	testAnnotationProcessor 'org.projectlombok:lombok'

	implementation 'com.google.guava:guava:33.2.1-jre'
	implementation 'org.apache.commons:commons-collections4:4.4'
	implementation 'org.apache.commons:commons-lang3:3.12.0'

}

dependencyManagement {
	imports {
		mavenBom "org.springframework.modulith:spring-modulith-bom:${springModulithVersion}"
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	jvmArgs += '--enable-preview'

	useJUnitPlatform()
	testLogging {
		events "skipped", "failed"
	}

	maxParallelForks = Runtime.runtime.availableProcessors()

	systemProperty("junit.jupiter.conditions.deactivate", "*")
	systemProperty("junit.jupiter.extensions.autodetection.enabled", true)
	systemProperty("junit.jupiter.testinstance.lifecycle.default", "per_class")
	systemProperty("junit.jupiter.execution.parallel.enabled", true)
	systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		html.required = true
		xml.required = true
		csv.required = true
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: '**/generated/**')
		}))
	}
}
